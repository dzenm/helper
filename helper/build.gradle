apply plugin: 'com.android.library'

//添加这两行
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"
    dataBinding { enabled true }
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}
dependencies {
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0-rc02"
}

def siteUrl = 'https://github.com/dzenm/helper'         // 项目主页
def gitUrl = 'https://github.com/dzenm/helper.git'      // 项目的git地址
def libName = "helper"                                  // 发布到JCenter上的项目名字

// 布到组织名称名字, 必须填写, （com.dzenm.helper）这样写是不好的, 项目名会拼上去
// implementation 'com.dzenm.helper:helper:1.0.0'
group = "com.dzenm"
// 版本号，下次更新是只需要更改版本号即可
version = "1.0.14"
// 上面配置后上传至JCenter后的编译路径是这样的: implementation 'com.dzenm.helper:helper:1.0.0'

task sourcesJar(type: Jar) {                            // 生成源文件
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {                                             // 上传到JCenter所需要的源码文件
    archives sourcesJar
}

install {                                               // 配置maven库，生成POM.xml文件
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                //项目描述，随意填
                name 'Input text with fireworks.'
                url siteUrl
                licenses {
                    license {
                        //开源协议
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        //开发者的个人信息
                        id 'dzenm'
                        name 'dzenm'
                        email 'dinzhenyan1997@126.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

//上传到JCenter
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")   // 读取 local.properties 文件里面的 bintray.user
    key = properties.getProperty("bintray.apikey")  // 读取 local.properties 文件里面的 bintray.apikey
    configurations = ['archives']
    pkg {
        repo = "helper"                             // 注意：这里的repo值必须要和你创建Maven仓库的时候的名字一样
        name = libName                              // 发布到JCenter上的项目名字
        desc = 'Input text with fireworks'          // 项目描述
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}